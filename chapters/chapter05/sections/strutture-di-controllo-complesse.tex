\section{Definizione di effetti arbitrari}
Il meccanismo degli effetti algebrici è sufficientemente generale da permettere di definire effetti di qualunque tipo per modellare la logica applicativa secondo il livello di dettaglio più adatto.

Si consideri un'ultima volta l'esempio dell'accesso a un database riportato alle \Cref{sec:mtl-effetti-arbitrari,sec:free-effetti-arbitrari}, questo effetto può essere definito in Koka come:
\begin{lstlisting}[language=koka]
effect user-store
  ctl get(id: user-id): optional<user>
  ctl save(user: user): ()
  ctl delete(id: user-id): ()
\end{lstlisting}

Queste operazioni possono essere direttamente utilizzate come normali funzioni e i tipi e gli effetti saranno inferiti automaticamente:
\begin{lstlisting}[language=koka]
fun update-or-delete(user-id, f)
  match get(user-id)
    Nothing    -> ()
    Just(user) -> match f(user)  
      Update(updated) -> save(updated)
      Delete          -> delete(user-id)

fun update-age(user-id)
  with user <- update-or-delete(user-id)
  if user.age < 18 then Delete else
    Update(user(age = user.age + 1))
\end{lstlisting}

Come ci si può aspettare, la funzione \lstinline{update-age} avrà tipo \lstinline{(user-id) -> user-store ()}.
Se si provasse a invocare questa funzione direttamente nel \lstinline{main}, il compilatore solleverebbe un'eccezione dato che l'effetto dello \lstinline{user-store} non è stato gestito da un opportuno handler.

Anche in questo caso diventa facile realizzare un handler che usi una lista di utenti in memoria per poter testare la logica applicativa della funzione \lstinline{update-age}; per esempio:
\begin{lstlisting}[language=koka]
fun mocked-users(initial-users, computation)
  fun has-id(id)
    fn (user) user.user-id.value == id.value

  var users := initial-users
  handle(computation)
    return(_) -> users
    ctl get(id) ->
      val user = users.find(has-id(id))
      resume(user)
    ctl save(user) -> 
      users := users.remove(has-id(user.user-id))
      users := Cons(user, users)
      resume(())
    ctl delete(id) ->
      users := users.remove(has-id(id))
      resume(())
\end{lstlisting}

L'handler modifica internamente una lista di utenti in base alle operazioni dello \lstinline{user-store}. Anche in questo caso la funzione \lstinline{mocked-user} è inferita essere pura dato che la mutazione non è osservabile esternamente.

Questo permette di scrivere test completamente deterministici che non necessitano di collegarsi effettivamente ad alcun database:
\begin{lstlisting}[language=koka]
fun test-update-age-removes-underage-users()
  val users = [User(User-id(1), "Koka", 17)]
  val final-users = mocked-users(users) { update-age(User-id(1)) }
  assert("underage user was removed", final-users.is-empty)
\end{lstlisting}

%\subsection{Meccanismi di controllo di flusso complessi}
%Il meccanismo degli effetti algebrici è una generalizzazione delle eccezioni; come mostrato dagli esempi riportati, è stato possibile definire in maniera concisa meccanismi analoghi al \lstinline{try-catch-finally} di linguaggi di programmazione come Java che invece richiederebbero un supporto specifico da parte del compilatore.

%Grazie all'utilizzo degli effetti algebrici è possibile implementare in \emph{user space} meccanismi di controllo di flusso complessi come generatori e concorrenza strutturata (nello stile \lstinline{async-await})~\cite{cit:structured-asynchrony-with-algebraic-effects}.

%In seguito è riportato un esempio di come sia possibile realizzare dei generatori semplicemente sfruttando la possibilità di definire effetti algebrici.

%\subsubsection{Generatori}
%Numerosi linguaggi come JavaScript, TypeScript, Python e C\# offrono supporto diretto per poter definire dei generatori tramite le cosiddette \emph{generator function}~\cite{cit:yield-statement-c-sharp,cit:yield-statement-javascript, cit:yield-statement-python}.

%Considerando come esempio JavaScript è possibile definire una generator function come segue:
%\begin{lstlisting}[language=javascript]
%function* make-generator() {
%  yield 1;
%  yield 2;
%  yield 3;
%}
%\end{lstlisting}
%Quando una funzione definita in questo modo viene invocata il suo corpo non viene direttamente eseguito restituendo invece un generatore.

%Ogni qualvolta sia richiesto un nuovo valore dal generatore, il codice contenuto nel corpo della funzione viene eseguito fino al raggiungere uno \lstinline{yield} restituendo il valore fornito. L'esecuzione viene nuovamente messa in pausa fino a quando non viene richiesto nuovamente un valore:
%\begin{lstlisting}[language=javascript]
%const gen = make-generator();
%gen.next(); // returns 1
%gen.next(); // returns 2
%gen.next(); // returns 3
%\end{lstlisting}

%La sintassi fornita da \lstinline{yield} è senz'altro utile per poter definire iteratori complessi in maniera semplice. L'implementazione di tale meccanismo in questi linguaggi richiede di definire nuove parole chiave -- generalmente \lstinline{yield} -- e uno specifico supporto da parte del compilatore che deve trasformare il codice della funzione in una macchina a stati~\cite{cit:iterators-technical-implementation}.

%Gli effetti algebrici sono sufficientemente generali per poter definire i generatori nello stile di JavaScript come semplice libreria, senza richiedere supporto ad hoc da parte del compilatore:


