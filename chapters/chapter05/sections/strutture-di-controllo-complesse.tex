\section{Definizione di effetti arbitrari}
Il meccanismo degli effetti algebrici è sufficientemente generale da permettere di definire effetti di qualunque tipo per modellare la logica applicativa secondo il livello di dettaglio più adatto.

Si consideri un'ultima volta l'esempio dell'accesso a un database riportato alle \Cref{sec:mtl-effetti-arbitrari,sec:free-effetti-arbitrari}, questo effetto può essere definito in Koka come:
\begin{lstlisting}[language=koka]
effect user-store
  ctl get(id: user-id): optional<user>
  ctl save(user: user): ()
  ctl delete(id: user-id): ()
\end{lstlisting}

Queste operazioni possono essere direttamente utilizzate come normali funzioni e i tipi e gli effetti saranno inferiti automaticamente:
\begin{lstlisting}[language=koka]
fun update-or-delete(user-id, f)
  match get(user-id)
    Nothing    -> ()
    Just(user) -> match f(user)  
      Update(updated) -> save(updated)
      Delete          -> delete(user-id)

fun update-age(user-id)
  with user <- update-or-delete(user-id)
  if user.age < 18 then Delete else
    Update(user(age = user.age + 1))
\end{lstlisting}

Come ci si può aspettare, la funzione \lstinline{update-age} avrà tipo \lstinline{(user-id) -> user-store ()}.
Se si provasse a invocare questa funzione direttamente nel \lstinline{main}, il compilatore solleverebbe un'eccezione dato che l'effetto dello \lstinline{user-store} non è stato gestito da un opportuno \term{handler}.
Infatti, l'unico effetto che il \lstinline{main} può avere è quello di effettuare input e output; qualunque altro effetto deve essere prima stato gestito da un opportuno \term{handler}. In questo modo si ha la garanzia che non ci possano essere effetti che inavvertitamente sono stati ignorati.

Anche in questo caso diventa facile realizzare un \term{handler} che usi una lista di utenti in memoria per poter testare la logica applicativa della funzione \lstinline{update-age}; per esempio:
\begin{lstlisting}[language=koka]
fun mocked-users(initial-users, computation)
  fun has-id(id)
    fn (user) user.user-id.value == id.value

  var users := initial-users
  handle(computation)
    return(_) -> users
    ctl get(id) ->
      val user = users.find(has-id(id))
      resume(user)
    ctl save(user) -> 
      users := users.remove(has-id(user.user-id))
      users := Cons(user, users)
      resume(())
    ctl delete(id) ->
      users := users.remove(has-id(id))
      resume(())
\end{lstlisting}

L'\term{handler} modifica internamente una lista di utenti in base alle operazioni dello \lstinline{user-store}. Anche in questo caso l'utilizzo della variabile non è osservabile esternamente e la funzione è pura.

Questo permette di scrivere test completamente deterministici che non necessitano di collegarsi effettivamente ad alcun database:
\begin{lstlisting}[language=koka]
fun test-update-age-removes-underage-users()
  val users = [User(User-id(1), "Koka", 17)]
  val final-users = mocked-users(users) { update-age(User-id(1)) }
  assert("underage user was removed", final-users.is-empty)
\end{lstlisting}

\subsection{Meccanismi di controllo di flusso complessi}
Gli effetti algebrici sono una generalizzazione delle eccezioni; come mostrato dagli esempi riportati, è stato possibile definire in maniera concisa meccanismi analoghi al \lstinline{try-catch-finally} di linguaggi di programmazione come Java che invece richiederebbero un supporto specifico da parte del compilatore.

Inoltre, grazie all'utilizzo degli effetti algebrici è possibile implementare in \term{user space} altre tecniche di controllo di flusso complesse come generatori e concorrenza strutturata (nello stile \emph{async-await})~\cite{cit:structured-asynchrony-with-algebraic-effects}.

Si consideri l'esempio dei generatori: numerosi linguaggi come JavaScript, TypeScript, Python e C\# hanno introdotto la possibilità di definire generatori tramite le cosiddette \term{generator function}~\cite{cit:yield-statement-c-sharp,cit:yield-statement-javascript,cit:yield-statement-python} e sintassi ad hoc.

Tuttavia, per poter utilizzare tale meccanismo, è richiesto un supporto diretto da parte del compilatore il quale dovrà trasformare il corpo delle \term{generator function} in macchine a stati~\cite{cit:iterators-technical-implementation}. Inoltre, nell'implementare tali trasformazioni, è fondamentale prestare attenzione al modo in cui potrebbero interferire con altri meccanismi come le eccezioni e l'asincronia.

Nel caso degli effetti non è necessaria alcuna estensione al linguaggio in quanto tutti questi meccanismi possono essere generalizzati da opportuni effetti algebrici e \term{handler}.

Il compilatore quindi dovrà supportare e ottimizzare un singolo meccanismo -- quello degli effetti -- e tutti gli altri potranno essere definiti sulla base di questo come librerie.

\subsubsection{Effetto del non determinismo}
Per mostrare il potere espressivo degli effetti algebrici prenderemo in considerazione un ultimo esempio: quello del non determinismo.

L'effetto della scelta non deterministica può essere modellato come segue:
\begin{lstlisting}[language=koka]
effect choice<a>
  ctl or(one: a, other: a): a
\end{lstlisting}
L'effetto \lstinline{choice} rappresenta una computazione che può non deterministicamente scegliere una delle opzioni a disposizione.
Per esempio, è possibile modellare il lancio di una moneta come:
\begin{lstlisting}[language=koka]
type coin
  Heads
  Tails

fun flip()
  Heads.or(Tails)
\end{lstlisting}
Il tipo inferito per \lstinline{flip} è \lstinline{() -> choice<coin> coin}. Questa funzione può essere utilizzata per accumulare i risultati di lanci di più monete in una lista:
\begin{lstlisting}[language=koka]
fun fill(n, action)
  if n <= 0 then [] else
    Cons(action(), fill(n - 1, action))

fun flips(times)
  fill(times) { flip() }
\end{lstlisting}
L'utilità di definire l'effetto del non determinismo in maniera astratta sta nella possibilità di poter scegliere la semantica più adatta allo specifico caso. In questo caso si possono realizzare diversi \term{handler}; per esempio un primo \term{handler} potrebbe restituire un risultato costante:
\begin{lstlisting}[language=koka]
fun constant-coin(coin: coin, computation)
  handle(computation)
    ctl or(_, _) -> resume(coin)
\end{lstlisting}
Questa funzione può essere utile per verificare il comportamento di funzioni che si basano sull'effetto del non determinismo e testarne i possibili percorsi di esecuzione in maniera deterministica.

Un handler più ``tradizionale'' potrebbe invece interpretare l'effetto della scelta restituendo casualmente una delle due opzioni:
\begin{lstlisting}[language=koka]
fun real-coin(computation)
  handle(computation)
    ctl or(one, other) ->
      resume(if random-bool() then one else other)
\end{lstlisting}
In questo caso si usa la funzione di libreria \lstinline{random-bool} che aggiunge l'effetto \lstinline{ndet} che permette di sfruttare un generatore di numeri pseudocasuali predefinito. È interessante osservare come un \term{handler} possa aggiungere effetti alla computazione che sta gestendo, nell'esempio viene rimosso l'effetto \lstinline{choice} sostituendolo con quello predefinito \lstinline{ndet}.

Da questo punto di vista un \term{handler} può essere visto come una sorta di compilatore che traduce chiamate ad operazioni astratte in operazioni sempre più di basso livello fino ad ottenere una funzione eseguibile direttamente dal \lstinline{main} (vale a dire con il solo effetto \lstinline{io} che può essere gestito automaticamente dal compilatore).

\subsubsection{Ripristinare l'esecuzione più volte}
In tutti gli esempi mostrati fino ad ora l'esecuzione della funzione che ha invocato un effetto è stata ripristinata al più una sola volta.
Tuttavia, \lstinline{resume} è una funzione e in quanto elemento di prima classe del linguaggio potrebbe essere invocata più volte, passata come argomento a una funzione, ecc.

Riprendendo l'esempio del non determinismo, è possibile ripristinare la computazione più volte con tutte le possibili alternative fra cui viene effettuata la scelta:
\begin{lstlisting}[language=koka]
fun all-outcomes(computation)
  handle(computation)
    return(x) -> [x]
    ctl or(one, other) -> resume(one) ++ resume(other)
\end{lstlisting}
In questo caso la computazione viene ripristinata una prima volta utilizzando come valore il primo fra cui scegliere, viene poi ripristinata una seconda volta utilizzando il secondo valore. I risultati ottenuti sono poi concatenati fra loro.

Grazia alla possibilità di riprendere la computazione più volte con valori differenti è possibile accumulare tutti i possibili risultati del lancio di una moneta:
\begin{lstlisting}[language=koka]
fun main()
  all-outcomes { 3.flips }
  // [
  //  [Heads, Heads, Heads],
  //  [Heads, Heads, Tails],
  //  [Heads, Tails, Heads],
  //  ...
  //  [Tails, Tails, Tails]
  // ]
\end{lstlisting}

Si noti la semplicità con cui è possibile cambiare l'\term{handler} per modificare la semantica delle operazioni senza cambiare in alcun modo la definizione delle operazioni di base come \lstinline{flip} e \lstinline{flips}.
